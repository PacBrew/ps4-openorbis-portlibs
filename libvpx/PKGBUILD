# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libvpx
pkgver=1.8.0
pkgrel=2
pkgdesc='A free software video codec library from Google and the Alliance for Open Media (AOMedia). It serves as the reference software implementation for the VP8 and VP9 video coding formats.'
arch=('any')
url='https://chromium.googlesource.com/webm/libvpx'
license=('BSD')
options=(!strip libtool staticlibs)
source=("https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz")
sha256sums=('86df18c694e1c06cc8f83d2d816e9270747a0ce6abe316e93a4f4095689373f6')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd libvpx-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" --target=generic-gnu \
    --disable-shared --enable-static \
    --disable-examples --disable-tools \
    --disable-docs --disable-unit-tests

  make
}

package() {
  cd libvpx-$pkgver
  make DESTDIR="$pkgdir" install
}
