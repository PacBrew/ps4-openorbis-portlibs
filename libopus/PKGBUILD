# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libopus
pkgver=1.3
pkgrel=1
pkgdesc='Reference implementation of the lossy audio codec'
arch=('any')
url='https://opus-codec.org/'
license=(BSD)
options=(!strip libtool staticlibs)
source=("https://archive.mozilla.org/pub/opus/opus-$pkgver.tar.gz")
sha256sums=('4f3d69aefdf2dbaf9825408e452a8a414ffc60494c70633560700398820dc550')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
groups=('ps4-openorbis-portlibs')

build() {
  cd opus-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-doc --disable-extra-programs

  make
}

package() {
  cd opus-$pkgver
  
  make DESTDIR="$pkgdir" install
  
  # remove useless stuff
  source /opt/pacbrew/ps4/openorbis/ps4vars.sh
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}

