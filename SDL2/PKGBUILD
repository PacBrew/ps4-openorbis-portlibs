# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2
pkgver=2.0.18
pkgrel=4
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/PacBrew/SDL/archive/2cab9c8372fd4df30ac9af2d56a02264db6fc3e2.zip")
sha256sums=('2717e137a3cd24b94e54921e0c5b5cf73e768f4115eac4e4b8c66a646b2552b2')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL-2cab9c8372fd4df30ac9af2d56a02264db6fc3e2

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    ..

  make
}

package() {
  cd SDL-2cab9c8372fd4df30ac9af2d56a02264db6fc3e2/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}
