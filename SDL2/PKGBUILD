# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2
pkgver=2.0.18
pkgrel=5
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/PacBrew/SDL/archive/d83d9d19cea594951ff41285be11dd6b44d1be28.zip")
sha256sums=('ae8dcbf7bff9d544d5ce44912ac6b786c2d1919f6196392a6c5bbc61a532bc26')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL-d83d9d19cea594951ff41285be11dd6b44d1be28

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    ..

  make
}

package() {
  cd SDL-d83d9d19cea594951ff41285be11dd6b44d1be28/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}
