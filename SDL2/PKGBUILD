# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2
pkgver=2.0.18
pkgrel=8
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
gitcommit=9205f48e86cc1e74a09da746279e98166043c9ac
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/PacBrew/SDL/archive/$gitcommit.zip")
sha256sums=('614c21a428f5d7ca657fb4bd687bc7e317c250739db7f731a9559d5d6e594346')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL-$gitcommit

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

package() {
  cd SDL-$gitcommit/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}
