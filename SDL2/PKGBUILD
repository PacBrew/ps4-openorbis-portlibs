# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2
pkgver=2.0.18
pkgrel=9
pkgdesc="A library for portable low-level access to video, audio, joysticks..."
gitcommit=911ed94116d6277e768d1fdff4ea06717094ccb5
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/PacBrew/SDL/archive/$gitcommit.zip")
sha256sums=('ee4e19ac07941cc8900866fcdb88ef0bcf832f8aa919604330d30eec27fe1371')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL-$gitcommit

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

package() {
  cd SDL-$gitcommit/build

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}
