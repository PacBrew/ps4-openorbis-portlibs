# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-mbedtls
pkgver=2.16.6
pkgrel=2
pkgdesc='An open source, portable, easy to use, readable and flexible SSL library'
arch=('any')
url='https://tls.mbed.org'
license=('apache')
options=(!strip libtool staticlibs)
depends=(ps4-openorbis-zlib)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://tls.mbed.org/download/mbedtls-${pkgver}-apache.tgz")
groups=('ps4-openorbis-portlibs')
sha256sums=('66455e23a6190a30142cdc1113f7418158839331a9d8e6b0778631d077281770')

build() {
  cd mbedtls-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir build && cd build

  openorbis-cmake \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    -DENABLE_ZLIB_SUPPORT=TRUE \
    -DENABLE_TESTING=FALSE -DENABLE_PROGRAMS=FALSE \
    ..

  make

}

package() {
  cd mbedtls-$pkgver/build
  make install DESTDIR="$pkgdir"
}
