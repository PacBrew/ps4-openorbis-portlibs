# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libpng
pkgver=1.6.37
pkgrel=3
pkgdesc='PNG format graphic files library'
arch=('any')
url='http://www.libpng.org'
license=('custom')
source=("https://download.sourceforge.net/libpng/libpng-$pkgver.tar.xz")
sha256sums=('505e70834d35383537b6491e7ae8641f1a4bed1876dbfe361201fc80868d88ca')
options=(!strip libtool staticlibs)
depends=('ps4-openorbis-zlib')
makedepends=('ps4-openorbis-vars')
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd libpng-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  sed -i 's/^bin_PROGRAMS = .*//' Makefile.in
  sed -i '103,105d' Makefile.in

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static

  make
}

package() {
  cd libpng-$pkgver
  make LN_S=cp DESTDIR="$pkgdir" install
}
