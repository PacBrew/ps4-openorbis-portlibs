# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libfribidi
pkgver=1.0.4
pkgrel=1
pkgdesc='Free Implementation of the Unicode Bidirectional Algorithm'
arch=('any')
url="https://github.com/fribidi/fribidi"
license=('LGPL2.1')
options=(!strip staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
source=("https://github.com/fribidi/fribidi/releases/download/v$pkgver/fribidi-$pkgver.tar.bz2")
sha256sums=('94bdfe553e004d8bd095b109e182682311dd511740d5083326d1582f1df237be')
groups=('ps4-openorbis-portlibs')

prepare() {
  cd fribidi-$pkgver
  # patch out binaries, as they are not needed
  sed '/^SUBDIRS/ s/bin //' -i Makefile.am
  ./autogen.sh
}

build() {
  cd fribidi-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static

  make
}

package() {
  cd fribidi-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # remove documentation
  rm -fr "$pkgdir"${OPENORBIS}/usr/share
}
