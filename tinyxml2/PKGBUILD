# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-tinyxml2
pkgver=9.0.0
pkgrel=2
pkgdesc='A simple, small, efficient, C++ XML parser that can be easily integrated into other programs'
arch=('any')
url='http://leethomason.github.io/tinyxml2/'
license=('apache')
options=(!strip !debug libtool staticlibs)
source=( "https://github.com/leethomason/tinyxml2/archive/${pkgver}.tar.gz" )
sha256sums=('cc2f1417c308b1f6acc54f88eb70771a0bf65f76282ce5c40e54cfe52952702c')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd tinyxml2-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  mkdir -p build && cd build

  openorbis-cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${OPENORBIS}/usr \
    -DBUILD_SHARED_LIBS=OFF -DBUILD_TESTING=OFF \
    -DCMAKE_C_FLAGS="$CFLAGS" \
    -DCMAKE_CXX_FLAGS="$CFLAGS $CXXFLAGS -D__APPLE__" \
    -DCMAKE_CXX_STANDARD_LIBRARIES="-lc++" \
    -DCMAKE_BUILD_TYPE=Release \
    ..

  make
}

package() {
  cd tinyxml2-$pkgver/build
  make DESTDIR="$pkgdir" install
}
