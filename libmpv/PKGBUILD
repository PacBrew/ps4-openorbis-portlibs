# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libmpv
pkgver=0.34.1
pkgrel=5
pkgdesc='Command line video player (library only)'
arch=('any')
url='https://mpv.io/'
license=('GPL')
options=(!strip libtool staticlibs)
source=("libmpv-${pkgver}.tar.gz::https://github.com/mpv-player/mpv/archive/v${pkgver}.tar.gz" "mpv.patch")
sha256sums=(
  '32ded8c13b6398310fa27767378193dc1db6d78b006b70dbcbd3123a1445e746'
  '015a89cb23eaebd786765029013ae44cd8c6892c1433ac2d402cb0cb2203b323'
)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
depends=('ps4-openorbis-sdl2' 'ps4-openorbis-ffmpeg')
groups=('ps4-openorbis-portlibs')

prepare() {
  cd mpv-$pkgver
  patch -Np1 -i "$srcdir/mpv.patch"
}

build() {
  cd mpv-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./bootstrap.py

  TARGET=x86_64 ./waf configure --prefix="${OPENORBIS}/usr" \
    --disable-libmpv-shared --enable-libmpv-static --disable-cplayer \
    --disable-iconv --disable-jpeg --disable-libavdevice \
    --enable-sdl2 --enable-sdl2-audio --disable-sdl2-gamepad --disable-sdl2-video

  ./waf build
}

package() {
  cd mpv-$pkgver
  ./waf install --destdir=$pkgdir
}

