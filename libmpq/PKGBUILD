# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libmpq
pkgver=1.0.0
pkgrel=1
pkgdesc=''libmpq' is a library which can be easily used in own applications to extract, create or manipulate MoPaQ mpq archives.'
arch=('any')
url='https://github.com/diasurgical/libmpq'
license=(GPL2)
source=("$url/archive/0f10bd1600f406b13932bf5351ba713361262184.zip")
sha256sums=('cbd66458f1538dce523690db2a038ee3bef7627efe6936c8da4f2b9b0eec1c52')
options=(!strip libtool staticlibs)
depends=("ps4-openorbis-bzip2" "ps4-openorbis-zlib")
groups=('ps4-openorbis-portlibs')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')

build() {
  cd libmpq-0f10bd1600f406b13932bf5351ba713361262184

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./autogen.sh
  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static

  # TODO: fix toolchain
  sed -i s'|<sys/endian.h>|<endian.h>|'g libmpq/endian.h

  make
}

package() {
  cd libmpq-0f10bd1600f406b13932bf5351ba713361262184
  make DESTDIR="$pkgdir" install

  # remove useless stuff
  source /opt/pacbrew/ps4/openorbis/ps4vars.sh
  rm -r "${pkgdir}/usr"
  rm -r "${pkgdir}/${OPENORBIS}/usr/share"
}
