# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libogg
pkgver=1.3.4
pkgrel=2
pkgdesc='The Ogg transport bitstream is designed to provide framing, error protection and seeking structure for higher-level codec streams that consist of raw, unencapsulated data packets, such as the Opus, Vorbis and FLAC audio codecs or the Theora and Dirac video codecs.'
arch=('any')
url='https://wiki.xiph.org/Ogg'
license=(Xiph.org)
options=(!strip !debug libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/ogg/libogg-$pkgver.tar.gz")
sha256sums=('fe5670640bd49e828d64d2879c31cb4dde9758681bb664f9bdbf159a01b0c76e')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd libogg-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static

  make
}

package() {
  cd libogg-$pkgver
  make DESTDIR="$pkgdir" install
}
