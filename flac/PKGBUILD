# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-flac
pkgver=1.3.2
pkgrel=1
pkgdesc='FLAC stands for Free Lossless Audio Codec, an audio format similar to MP3, but lossless...'
arch=('any')
url='https://xiph.org/flac/'
license=(Xiph.org)
options=(!strip libtool staticlibs)
source=("https://ftp.osuosl.org/pub/xiph/releases/flac/flac-$pkgver.tar.xz")
sha256sums=('91cfc3ed61dc40f47f050a109b08610667d73477af6ef36dcad31c31a4a8d53f')
depends=("ps4-openorbis-libogg")
makedepends=('ps4-openorbis-vars')
groups=('ps4-openorbis-portlibs')

build() {
  cd flac-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-xmms-plugin --enable-cpplibs \
    --disable-thorough-tests --disable-oggtest

  make -C src/libFLAC
}

package() {
  cd flac-$pkgver
  make -C src/libFLAC DESTDIR="$pkgdir" install
  make -C include/FLAC DESTDIR="$pkgdir" install
  make -C include/FLAC++ DESTDIR="$pkgdir" install
}
