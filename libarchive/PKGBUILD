# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-libarchive
pkgver=3.3.2
pkgrel=2
pkgdesc='library that can create and read several streaming archive format'
arch=('any')
url="https://www.libarchive.org"
license=('Simplified BSD')
options=(!strip libtool staticlibs)
source=(
 "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
 "libarchive-3.3.2.patch"
)
sha256sums=(
 'ed2dbd6954792b2c054ccf8ec4b330a54b85904a80cef477a1c74643ddafa0ce'
 'b7132d5ba5d399c0040727fdd3f0a9a36843a7d28fcb8826706e77c4f35e3d74'
)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
depends=('ps4-openorbis-bzip2' 'ps4-openorbis-zlib')
groups=('ps4-openorbis-portlibs')

build() {
  cd libarchive-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  patch -Np1 -i "$srcdir"/libarchive-${pkgver}.patch

  CFLAGS="${CFLAGS} -DHAVE_ARC4RANDOM_BUF" \
  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-bsdtar --disable-bsdcat \
    --disable-bsdcpio --disable-acl --without-xml2

  make -j1
}

package() {
  cd libarchive-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install
  # remove useless stuff
  rm -r "$pkgdir"${OPENORBIS}/usr/bin
  rm -r "$pkgdir"${OPENORBIS}/usr/share
}

