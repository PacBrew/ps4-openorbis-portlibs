# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2_ttf
pkgver=2.0.15
pkgrel=1
pkgdesc="SDL2 TrueType font library."
arch=('any')
url="https://libsdl.org/projects/SDL_ttf/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
depends=(
  'ps4-openorbis-sdl2'
  'ps4-openorbis-freetype'
)
source=("${url}release/SDL2_ttf-${pkgver}.tar.gz")
sha256sums=('a9eceb1ad88c1f1545cd7bd28e7cbc0b2c14191d40238f531a15b01b1b22cd33')
groups=('ps4-openorbis-portlibs' 'ps4-openorbis-sdl2')

build() {
  cd SDL2_ttf-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  # patch out compiling showfont and glfont
  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-sdltest --without-x

  make
}

package() {
  cd SDL2_ttf-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # add our static lib
  echo "Requires.private: freetype2" >> "${pkgdir}/${OPENORBIS}/usr/lib/pkgconfig/SDL2_ttf.pc"
}
