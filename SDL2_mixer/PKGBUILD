# Maintainer:  cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-sdl2_mixer
pkgver=2.0.4
pkgrel=2
pkgdesc="A sample multi-channel audio mixer library."
arch=('any')
url="https://libsdl.org/projects/SDL_mixer/"
license=("zlib")
options=(!strip libtool staticlibs)
source=("${url}release/SDL2_mixer-${pkgver}.tar.gz")
sha256sums=('b4cf5a382c061cd75081cf246c2aa2f9df8db04bdda8dcdc6b6cca55bede2419')
makedepends=('ps4-openorbis-pkg-config' 'ps4-openorbis-vars')
depends=(
  'ps4-openorbis-sdl2'
  'ps4-openorbis-flac'
  'ps4-openorbis-libvorbis'
  'ps4-openorbis-libmodplug'
  'ps4-openorbis-mpg123'
  'ps4-openorbis-opusfile'
)
groups=(
  'pacbrew' 'pacbrew-ps4' 'pacbrew-ps4-portlibs'
  'ps4-openorbis-portlibs' # for backward compatibility
)

build() {
  cd SDL2_mixer-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  # patch out compiling playwave and playmus
  sed 's|\$(objects)/play.*mus\$(EXE)||' -i Makefile.in

  ./configure --prefix="${OPENORBIS}/usr" --host=x86_64 \
    --disable-shared --enable-static \
    --disable-music-cmd \
    --enable-music-ogg \
    --enable-music-mod-modplug \
    --disable-music-mod-modplug-shared \
    --disable-music-ogg-shared \
    --disable-music-flac-shared \
    --disable-music-mp3-mpg123-shared \
    --disable-music-opus-shared

  make
}

package() {
  cd SDL2_mixer-$pkgver

  source /opt/pacbrew/ps4/openorbis/ps4vars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: vorbis libmodplug libmpg123 opusfile" >> "${pkgdir}/${OPENORBIS}/usr/lib/pkgconfig/SDL2_mixer.pc"
}
